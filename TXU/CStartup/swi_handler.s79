;----------------------------------------------------------------------
;
; SWI_HANDLER
;
; This module contains an example swi handler which is included in the
; libraries. If a user-defined swi handler needs to be written this module
; can serve as a template.
;
; Copyright 2000 IAR Systems. All rights reserved.
;
; $Revision: 1.3.1.2 $
;
;----------------------------------------------------------------------

		MODULE	?SWI_HANDLER

		RSEG	SWITAB:CODE:NOROOT(2)
		RSEG	ICODE:CODE:NOROOT(2)
		PUBLIC	__iar_swi_handler
		PUBLIC	__next_swi_in_chain

		CODE32				;; Always ARM mode

__iar_swi_handler:
		STMDB	SP!,{R0}		;; Save space for func. address
		STMDB	SP!,{R4-R8}     	;; Save used registers
		
		; arm or thumb swi number
		MRS	R4,SPSR
		TST	R4,#0x20		;; T-flag
		LDREQ	R5,[LR,#-4]		;; swi from arm mode
		BICEQ	R5,R5,#0xFF000000
		LDRNEH	R5,[LR,#-2]		;; swi from thumb mode
		BICNE	R5,R5,#0x0000FF00
		LDR	R4,=SFE(SWITAB)
		LDR	R6,=SFB(SWITAB)
loop:
		LDMIA	R6!,{R7,R8}
		CMP	R7,R5			;; found function?
		BEQ	function_found
		CMP	R6,R4			;; reached end of table
		BNE	loop
		LDR	R8,=__next_swi_in_chain	;; load next swi handler
		LDR	R8,[R8, #+0]
function_found:
		STR	R8,[SP, #+20]		;; Save function address
		LDMIA	SP!,{R4-R8,PC}

		LTORG

		RSEG	HUGE_Z:DATA:NOROOT(2)

		DATA
__next_swi_in_chain:
		DS8	4		
		
		ENDMOD

		END

